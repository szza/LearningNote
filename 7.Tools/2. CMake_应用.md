# ***`CMake`*** 使用
目录结构：
```bash
    Tutorial
    ├── CMakeLists.txt
    ├── selfmath
    │   ├── CMakeLists.txt
    │   ├── selfmath.cc
    │   └── selfmath.h
    ├── tutorial.cc          # 主程序文件
    └── TutorialConfig.h.in  # 配置文件
```
+ `Tutorial/CMakeLists.txt`
    ```cmake
        cmake_minimum_required(VERSION 3.10)
        project(Tutorial VERSION 1.0)

        # 指定c++11标准
        set(CMAKE_CXX_STANDARD 11)
        set(CMAKE_CXX_STANDARD_REQUIRED True)

        option(USE_MYPATH 
            "use tutorial provided math implementation"  ON)   
        configure_file(TutorialConfig.h.in  TutorialConfig.h) # 配置

        if(USE_MYPATH)
            #  这两步就可以搜索到子目录`selfmath`下的`.cc`文件生成的静态库`libselfmath.a`。
            add_subdirectory(selfmath)
            # 函数原型`list(APPEND <list> [<element>...])`：将`element`添加到`list`中。
            list(APPEND EXTRA_LIBS selfmath)    # 这是为了找到源文件
        endif(USE_MYPATH)

        add_executable(Tutorial tutorial.cc)
        # Tutorial使用到了`selfmath`中的函数，因此链接到`selfmath`这个库
        target_link_libraries(Tutorial ${EXTRA_LIBS})
        # 把build目录加入可执行文件`Tutorial`的头文件搜索目录。
        target_include_directories(Tutorial PUBLIC
                                "${PROJECT_BINARY_DIR}" ) # 把build目录加入 

        install(TARGETS Tutorial DESTINATION bin)
        install(FILES "${PROJECT_BINARY_DIR}/TutorialConfig.h" DESTINATION include)
    ```
+ `selfmath/CMakeLists.txt`
    ```cmake
        add_library(selfmath selfmath.cc)
        target_include_directories(selfmath 
                                INTERFACE ${CMAKE_CURRENT_SOURCE_DIR}
                                PRIVATE   ${CMAKE_BINARY_DIR})

        # 这里都是为了检测是否存在某个函数，如果存在则使用，不存在则使用库函数               
        include(CheckSymbolExists)
        set(CMAKE_REQUIRED_LIBRARIES  "m")
        check_symbol_exists(log "math.h" HAVE_LOG)
        check_symbol_exists(exp "math.h" HAVE_EXP)

        if(HAVE_LOG AND HAVE_EXP)
            target_compile_definitions(selfmath
                                    PRIVATE "HAVE_LOG" "HAVE_EXP")
        endif()


        install(TARGETS selfmath DESTINATION lib)
        install(FILES selfmath.h DESTINATION include)
    ```